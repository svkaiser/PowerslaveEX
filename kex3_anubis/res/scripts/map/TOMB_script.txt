//
// Copyright(C) 2014-2015 Samuel Villarreal
//
// This program is free software; you can redistribute it and/or
// modify it under the terms of the GNU General Public License
// as published by the Free Software Foundation; either version 2
// of the License, or (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// DESCRIPTION:
//      Level Script For TOMB.MAP
//

enum weaponTypes
{
    WP_MACHETE   = 0,
    WP_PISTOL,
    WP_M90,
    WP_BOMB,
    WP_FLAMETHROWER,
    WP_CSTAFF,
    WP_RA,
    WP_MANACLE,
    NUMWEAPONS
}

bool bScript1Triggered = false;
bool bPlayAlternateDialog = false;
bool bCasketOpened = false;
float bobTime = 0.0f;
kActor @ramses;

kVec3 ramsesOrigin(-1104, -2464, -704);

//
// EndRamsesEvent
//

void EndRamsesEvent(kActor @instigator)
{
    ramses.ChangeAnim("misc/ramses_vanish");
    
    Game.HaltMapScript(6);
    Game.HaltMapScript(7);
    Game.HaltMapScript(8);
    
    Game.StopMusic();
    Game.PlayMusic("music/egypt2.ogg", true);
    
    Game.FireRemoteEventFromTag(1020);
    Game.CallDelayedMapScript(3, instigator, 4.0f);
}

//
// GetRamsesDialog
//

float GetRamsesDialog(void)
{
    int16 arti = Player.Artifacts();
    
    if(arti == 0x3F) // has feather
    {
        if(bPlayAlternateDialog)
        {
            Game.PlayMusic("music/ramses/ra12.ogg", false);
            return 30.0f;
        }
        
        Game.PlayMusic("music/ramses/ra11.ogg", false);
        return 30.0f;
    }
    else if((arti & 0x10) != 0) // has scepter
    {
        Game.PlayMusic("music/ramses/ra10.ogg", false);
        return 28.0f;
    }
    else if((arti & 0x8) != 0) // has anklets
    {
        if(Player.WeaponOwned(WP_CSTAFF))
        {
            Game.PlayMusic("music/ramses/ra9.ogg", false);
            return 22.0f;
        }
        
        Game.PlayMusic("music/ramses/ra8.ogg", false);
        return 24.0f;
    }
    else if((arti & 0x4) != 0) // has shawl
    {
        if(Player.WeaponOwned(WP_FLAMETHROWER))
        {
            Game.PlayMusic("music/ramses/ra7.ogg", false);
            return 30.0f;
        }
        
        Game.PlayMusic("music/ramses/ra6.ogg", false);
        return 28.0f;
    }
    else if((arti & 0x2) != 0) // has mask
    {
        if(Player.WeaponOwned(WP_BOMB))
        {
            Game.PlayMusic("music/ramses/ra5.ogg", false);
            return 31.0f;
        }
        
        Game.PlayMusic("music/ramses/ra4.ogg", false);
        return 34.0f;
    }
    else if((arti & 0x1) != 0) // has sandals
    {
        Game.PlayMusic("music/ramses/ra3.ogg", false);
        return 38.0f;
    }
    
    if(bPlayAlternateDialog)
    {
        Game.PlayMusic("music/ramses/ra2.ogg", false);
        return 20.0f;
    }
    
    Game.PlayMusic("music/ramses/ra1.ogg", false);
    return 120.0f;
}

//
// script 0
//

$script 0
{
    Game.MoveScriptedSector(0, -256, -1, true);
}

//
// script 1
//

$script 1
{
    float dialogTime;
    
    if(bScript1Triggered)
    {
        return;
    }
    
    bScript1Triggered = true;
    
    Game.StopMusic();
    
    instigator.Velocity().Clear();
    instigator.Movement().Clear();
    
    if(bCasketOpened == false)
    {
        Game.MoveScriptedSector(0, 320, 4, true);
        bCasketOpened = true;
    }
    
    Player.HoldsterWeapon();
    Game.CallDelayedMapScript(2, instigator, 0);
    
    delay(0.5f);
    
    @ramses = Game.SpawnActor("Obj_Ramses", ramsesOrigin.x, ramsesOrigin.y, ramsesOrigin.z, 0, 14);
    bobTime = 0.0f;
    
    delay(1.0f);
    dialogTime = GetRamsesDialog();
    
    Game.CallDelayedMapScript(4, instigator, 2.0f);
    Game.CallDelayedMapScript(5, instigator, dialogTime);
    Game.CallDelayedMapScript(6, instigator, 1.0f);
    Game.CallDelayedMapScript(7, instigator, 1.0f);
    Game.CallDelayedMapScript(8, instigator, 1.0f);
}

//
// script 2
//

$script 2
{
    kVec3 dir;
    
    Player.LockTime() = 8;
    dir = (ramsesOrigin - instigator.Origin()).Normalize();
    
    instigator.Yaw() = (dir.ToYaw() - instigator.Yaw()) * 0.0625f + instigator.Yaw();
    instigator.Pitch() = (-dir.ToPitch() - instigator.Pitch()) * 0.0625f + instigator.Pitch();
    
    instigator.Origin().Lerp(kVec3(-1088.0f, -2794.7f, -864.0f), 0.0625f);
    
    $restart;
}

//
// script 3
//

$script 3
{
    bScript1Triggered = false;
    bPlayAlternateDialog = true;
}

//
// script 4
//

$script 4
{
    if((Player.Buttons() & (BC_USE|BC_ATTACK|BC_JUMP)) != 0)
    {
        Game.HaltMapScript(2);
        Game.HaltMapScript(5);
        
        EndRamsesEvent(instigator);
        return;
    }
    
    $restart;
}

//
// script 5
//

$script 5
{
    Game.HaltMapScript(2);
    Game.HaltMapScript(4);
    
    EndRamsesEvent(instigator);
}

//
// script 6
//

$script 6
{
    if((Math::RandByte() & 1) == 0)
    {
        ramses.SetFrameID(0);
    }
    else
    {
        ramses.SetFrameID(6);
    }
    
    delay(2.0f + (Math::RandFloat() * 2.0f));
    $restart;
}

//
// script 7
//

$script 7
{
    int frame;
    
    delay(4.0f + (Math::RandFloat() * 4.0f));
    frame = ramses.FrameID();
    
    if(frame == 6)
    {
        ramses.SetFrameID(9);
    }
    else
    {
        ramses.SetFrameID(3);
    }
    
    delay(0.125f);
    
    if(frame == 6)
    {
        ramses.SetFrameID(6);
    }
    else
    {
        ramses.SetFrameID(0);
    }
    
    $restart;
}

//
// script 8
//

$script 8
{
    float time = bobTime / 4.0f;
    
    ramses.Origin().z = ramsesOrigin.z + (8.0f * Math::Sin(time * 0.1f));
    bobTime += 1.0f;
    
    $restart;
}
